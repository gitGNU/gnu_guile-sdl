dnl Process this file with autoconf to produce configure.

AC_INIT([guile-sdl],[0.3.6],[ttn@gnuvola.org])
AC_CONFIG_AUX_DIR([am])
AC_CONFIG_HEADERS(config.h)
AC_CONFIG_SRCDIR(src/sdl.c)
AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE

##----------------------------------------------------------------------------
##: compiler

AC_PROG_CC

## If using GCC, ask for warnings and possibly a bit of quickness.
if test x"$GCC" = xyes ; then
  CFLAGS="$CFLAGS -Wall -Wno-long-long -pipe"
fi

##----------------------------------------------------------------------------
##: GUILE

GUILE_PROGS
GUILE_FLAGS

CPPFLAGS="$GUILE_CFLAGS"

GUILE_C2X_METHOD([c2x])

AC_MSG_CHECKING([whether twerp2texi can take option --scheme-implied])
if guile-tools twerp2texi --help | grep -q scheme-implied ; then
  TWERP2TEXIFLAGS=--scheme-implied
  AC_MSG_RESULT([yes])
else
  TWERP2TEXIFLAGS=
  AC_MSG_RESULT([no])
fi
AC_SUBST(TWERP2TEXIFLAGS)

AC_CHECK_HEADERS([guile/modsup.h])

AH_BOTTOM([
#ifdef HAVE_GUILE_MODSUP_H
#include <guile/modsup.h>
#else
#include "modsup.h"
#endif
])

GUILE_LIBSITE_DIR

##----------------------------------------------------------------------------
##: SDL

AM_PATH_SDL([1.2.0], :,
            AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!]))

CFLAGS="$CFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

dnl Checks for libraries.
dnl MUST have SDL_image
AC_CHECK_LIB(SDL_image, IMG_Load,
 	     LIBS="$LIBS -lSDL_image",
 	     AC_MSG_ERROR([*** SDL_image library not found!]))

dnl Bindings for optional libs.
GUILE_SDL_OPTLIB([mixer],[Mix_OpenAudio])
GUILE_SDL_OPTLIB([ttf],[TTF_Init])

##----------------------------------------------------------------------------
##: misc

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_DISABLE_STATIC	dnl module only
AC_PROG_LIBTOOL

GUILE_MODULE_CATALOG_PREP

##----------------------------------------------------------------------------
##: write it out

AC_CONFIG_FILES([
  Makefile
  src/Makefile
  sdl/Makefile
  sdl-mixer/Makefile
  sdl-ttf/Makefile
  sdl-gfx/Makefile
  sdl-gfx/SDL_gfx/Makefile
  doc/Makefile
])

AC_CONFIG_FILES([wily-guile],[chmod +x wily-guile])

AC_OUTPUT

dnl configure.in ends here
