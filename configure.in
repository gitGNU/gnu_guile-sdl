dnl Process this file with autoconf to produce configure.

AC_INIT(guile-sdl, 0.1.8)
AC_CONFIG_HEADERS(config.h)
AC_CONFIG_SRCDIR(src/sdl.c)
AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE

##----------------------------------------------------------------------------
##: compiler

AC_PROG_CC
AC_PROG_CPP

## If using GCC, ask for warnings and possibly a bit of quickness.
if test x"$GCC" = xyes ; then
  CFLAGS="$CFLAGS -Wall -ansi -pedantic -pipe"
fi

##----------------------------------------------------------------------------
##: GUILE

GUILE_PROGS
GUILE_FLAGS

CPPFLAGS="$GUILE_CFLAGS"

AC_MSG_CHECKING([c2x method])
if guile-tools | grep -q c2x ; then
  c2x="guile-tools c2x"
else
  c2x="guile-snarf"
fi
AC_SUBST(c2x)
AC_MSG_RESULT([$c2x])

AC_CHECK_HEADERS([guile/modsup.h])

AC_MSG_CHECKING([where to install the compiled modules])
guilelibsitedir="`guile-config re-prefix-info pkglibdir 2>/dev/null`"
if test x"$guilelibsitedir" = x ; then
   AC_MSG_RESULT([unknown])
   AC_MSG_ERROR([Sorry, cannot determine where to install.])
fi
guilelibsitedir="$guilelibsitedir"/site
AC_SUBST(guilelibsitedir)
AC_MSG_RESULT([$guilelibsitedir])

##----------------------------------------------------------------------------
##: MATHLIB

dnl Figure out which math library to use - taken from SDL_gfxPrimitives
case "$target" in
    *-*-cygwin* | *-*-mingw32*)
        MATHLIB=""
        ;;
    *-*-beos*)
        MATHLIB=""
        ;;
    *-*-darwin*)
        MATHLIB=""
        ;;
    *-*-aix*)
	if test x$ac_cv_prog_gcc = xyes; then
     	    CFLAGS="-mthreads"
	fi
        ;;
    alpha*-*-linux*)
    	CFLAGS="$CFLAGS -mcpu=ev4 -Wa,-mall"
	;;
    *)
        MATHLIB="-lm"
        ;;
esac
AC_SUBST(MATHLIB)

##----------------------------------------------------------------------------
##: SDL

SDL_VERSION=1.2.0
AM_PATH_SDL($SDL_VERSION, :,
            AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!]))

CFLAGS="$CFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

dnl Checks for libraries.
dnl MUST have SDL_image
AC_CHECK_LIB(SDL_image, IMG_Load,
 	     LIBS="$LIBS -lSDL_image",
 	     AC_MSG_ERROR([*** SDL_image library not found!]))

dnl SDL_mixer is optional
AC_ARG_ENABLE([mixer],
  AC_HELP_STRING([--disable-mixer],
                 [Omit bindings for SDL_mixer (default=enabled)]),
  , enable_mixer=yes)
if test x$enable_mixer = xyes ; then
  dnl We use ":" to avoid prepending to $LIBS (see sdl-mixer/Makefile.am).
  AC_CHECK_LIB(SDL_mixer, Mix_OpenAudio, :, enable_mixer=no)
fi
AM_CONDITIONAL([HAVE_MIXER], test x$enable_mixer = xyes)
if test x$enable_mixer = xyes ; then
  HAVE_MIXER=1
  AC_SUBST([HAVE_MIXER])
fi

dnl SDL_ttf is optional
AC_ARG_ENABLE([ttf],
  AC_HELP_STRING([--disable-ttf],
                 [Omit bindings for SDL_ttf (default=enabled)]),
 , enable_ttf=yes)
if test x$enable_ttf = xyes ; then
  dnl We use ":" to avoid prepending to $LIBS (see sdl-ttf/Makefile.am).
  AC_CHECK_LIB(SDL_ttf, TTF_Init, :, enable_ttf=no)
fi
AM_CONDITIONAL([HAVE_TTF], test x$enable_ttf = xyes)
if test x$enable_ttf = xyes ; then
  HAVE_TTF=1
  AC_SUBST([HAVE_TTF])
fi

##----------------------------------------------------------------------------
##: misc

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AM_PROG_LIBTOOL

##----------------------------------------------------------------------------
##: write it out

AC_CONFIG_FILES([
  Makefile
  src/Makefile
  src/SDL_rotozoom/Makefile
  sdl/Makefile
  sdl-mixer/Makefile
  sdl-ttf/Makefile
  sdl-gfx/Makefile
  sdl-gfx/SDL_gfxPrimitives/Makefile
  doc/Makefile
])

AC_CONFIG_COMMANDS([module-catalog-prep],[
  test -d .deps || mkdir .deps
  prereq=.deps/pre-inst.Pmerged-module-catalog
  test -f $prereq || echo '# dummy' > $prereq
  grep -q "include $prereq" Makefile || echo include $prereq >> Makefile
])

AC_OUTPUT

dnl configure.in ends here
