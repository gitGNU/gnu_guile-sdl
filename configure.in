dnl Process this file with autoconf to produce configure.

AC_INIT([guile-sdl], [0.3.1.50.4])
AC_CONFIG_HEADERS(config.h)
AC_CONFIG_SRCDIR(src/sdl.c)
AM_INIT_AUTOMAKE
AM_MAINTAINER_MODE

##----------------------------------------------------------------------------
##: compiler

AC_PROG_CC

## If using GCC, ask for warnings and possibly a bit of quickness.
if test x"$GCC" = xyes ; then
  CFLAGS="$CFLAGS -Wall -pedantic -Wno-long-long -pipe"
fi

##----------------------------------------------------------------------------
##: GUILE

GUILE_PROGS
GUILE_FLAGS

CPPFLAGS="$GUILE_CFLAGS"

AC_MSG_CHECKING([c2x method])
if guile-tools | grep -q c2x ; then
  c2x="guile-tools c2x"
else
  c2x="guile-snarf"
fi
AC_SUBST(c2x)
AC_MSG_RESULT([$c2x])

AC_CHECK_HEADERS([guile/modsup.h])

AH_BOTTOM([
#ifdef HAVE_GUILE_MODSUP_H
#include <guile/modsup.h>
#else
#include "modsup.h"
#endif
])

GUILE_SITE_DIR
GUILE_LIBSITE_DIR
## check for and work around guile 1.4.1.97 bug (sigh)
case "$GUILE_LIBSITE" in
  */site) ;;
  *) GUILE_LIBSITE=${GUILE_LIBSITE}/site ;;
esac

##----------------------------------------------------------------------------
##: MATHLIB

dnl Figure out which math library to use - taken from SDL_gfx
case "$target" in
    *-*-cygwin* | *-*-mingw32*)
        MATHLIB=""
        ;;
    *-*-beos*)
        MATHLIB=""
        ;;
    *-*-darwin*)
        MATHLIB=""
        ;;
    *-*-aix*)
	MATHLIB="-lm"
        if test x$ac_cv_prog_gcc = xyes; then
            CFLAGS="-mthreads"
	fi
        ;;
    *)
        MATHLIB="-lm"
        AC_PATH_X
        AC_PATH_XTRA
        ;;
esac
AC_SUBST(MATHLIB)

##----------------------------------------------------------------------------
##: SDL

SDL_VERSION=1.2.0
AM_PATH_SDL($SDL_VERSION, :,
            AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!]))

CFLAGS="$CFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

dnl Checks for libraries.
dnl MUST have SDL_image
AC_CHECK_LIB(SDL_image, IMG_Load,
 	     LIBS="$LIBS -lSDL_image",
 	     AC_MSG_ERROR([*** SDL_image library not found!]))

dnl SDL_mixer is optional
AC_ARG_ENABLE([mixer],
  AC_HELP_STRING([--disable-mixer],
                 [Omit bindings for SDL_mixer (default=enabled)]),
  , enable_mixer=yes)
if test x$enable_mixer = xyes ; then
  dnl We use ":" to avoid prepending to $LIBS (see sdl-mixer/Makefile.am).
  AC_CHECK_LIB(SDL_mixer, Mix_OpenAudio, :, enable_mixer=no)
fi
AM_CONDITIONAL([HAVE_MIXER], test x$enable_mixer = xyes)
if test x$enable_mixer = xyes ; then
  HAVE_MIXER=1
  AC_SUBST([HAVE_MIXER])
fi

dnl SDL_ttf is optional
AC_ARG_ENABLE([ttf],
  AC_HELP_STRING([--disable-ttf],
                 [Omit bindings for SDL_ttf (default=enabled)]),
 , enable_ttf=yes)
if test x$enable_ttf = xyes ; then
  dnl We use ":" to avoid prepending to $LIBS (see sdl-ttf/Makefile.am).
  AC_CHECK_LIB(SDL_ttf, TTF_Init, :, enable_ttf=no)
fi
AM_CONDITIONAL([HAVE_TTF], test x$enable_ttf = xyes)
if test x$enable_ttf = xyes ; then
  HAVE_TTF=1
  AC_SUBST([HAVE_TTF])
fi

##----------------------------------------------------------------------------
##: misc

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_DISABLE_STATIC	dnl module only
AC_LIBTOOL_TAGS([])
AC_PROG_LIBTOOL

top_builddir_absolute=`pwd`
AC_SUBST([top_builddir_absolute])

##----------------------------------------------------------------------------
##: write it out

AC_CONFIG_FILES([
  Makefile
  src/Makefile
  sdl/Makefile
  sdl-mixer/Makefile
  sdl-ttf/Makefile
  sdl-gfx/Makefile
  sdl-gfx/SDL_gfx/Makefile
  doc/Makefile
])

AC_CONFIG_FILES([wily-guile],[chmod +x wily-guile])

AC_CONFIG_COMMANDS([module-catalog-prep],[
  test -d .deps || mkdir .deps
  prereq=.deps/pre-inst.Pmerged-module-catalog
  test -f $prereq || echo '# dummy' > $prereq
  grep -q "include $prereq" Makefile || echo include $prereq >> Makefile
])

AC_OUTPUT

dnl configure.in ends here
